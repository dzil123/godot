#!/usr/bin/env python

Import("env")
Import("env_modules")

env_tracy = env_modules.Clone()

tracy_dir = "./tracy/public/"

tracy_src = [
    "TracyClient.cpp",
]

tracy_sources = [tracy_dir + file for file in tracy_src]

# Treat headers as system headers to avoid raising warnings. Not supported on MSVC.
if not env.msvc:
    env_tracy.Append(CPPFLAGS=["-isystem", Dir(tracy_dir).path])
else:
    env_tracy.Prepend(CPPPATH=[tracy_dir])

env_tracy.disable_warnings()


def env_append(**kwargs):
    env_tracy.Append(**kwargs)
    env_modules.Append(**kwargs)
    env.Append(**kwargs)


env_append(
    CPPDEFINES=["TRACY_ENABLE"],
    CPPFLAGS=["-fno-omit-frame-pointer"],
    LINKFLAGS=["-rdynamic"],
)

env_tracy.add_source_files(env.modules_sources, tracy_sources)
env_tracy.add_source_files(env.modules_sources, "*.cpp")
